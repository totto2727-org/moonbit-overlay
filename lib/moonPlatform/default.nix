# moonPlatform
# Basic strategy:
# 1. Parse root moon.mod and list all dependencies
#    ./parseMoonIndex.nix
#    ./listAllDependencies.nix
# 2. Fetch all dependencies into $MOON_HOME/registry/cache
# 3. Bundle core, toolchains and cached registry
# 4. Build moon package with bundled $MOON_HOME
{
  lib,
  fetchurl,
  stdenv,
  symlinkJoin,
  makeWrapper,
  system,
  callPackage,
  runCommand,
  writeText,
  zig,
  clang,
  pkg-config,
  # manually
  versions,
}:
{
  # public API
  version,
}:
let
  inherit (import ../utils.nix { inherit stdenv lib; }) mkToolChainsUri mkCoreUri target;

  moon-patched = callPackage ../moon-patched {
    rev = versions.${version}.moonRev;
    hash = versions.${version}.moonHash;
  };

  toolchains = callPackage ../toolchains.nix {
    inherit version moon-patched;
    url = mkToolChainsUri version;
    hash = versions."${version}"."${target}-toolchainsHash";
  };

  core = callPackage ../core.nix {
    inherit version;
    url = mkCoreUri version;
    hash = versions."${version}".coreHash;
  };

  fetchMoonPackage = import ./fetchMoonPackage.nix {
    inherit fetchurl;
  };

  parseMoonIndex = import ./parseMoonIndex.nix {
    inherit lib;
  };

  listAllDependencies = import ./listAllDependencies.nix {
    inherit parseMoonIndex lib;
  };

  buildCachedRegistry = import ./buildCachedRegistry.nix {
    inherit
      fetchMoonPackage
      listAllDependencies
      lib
      stdenv
      ;
  };

  moonModToJson = import ./moonModToJson.nix {
    inherit
      buildCachedRegistry
      bundleWithRegistry
      runCommand
      stdenv
      writeText
      ;
  };

  bundleWithRegistry = import ./bundleWithRegistry.nix {
    inherit
      symlinkJoin
      makeWrapper
      toolchains
      core
      ;
  };

  buildMoonPackage = import ./buildMoonPackage.nix {
    inherit
      lib
      stdenv
      buildCachedRegistry
      bundleWithRegistry
      moonModToJson
      ;
  };

  # Fine-grained, per-package builders (the crate2nix/cargo2nix analogue): an
  # external planner emits one call per package, wiring deps through derivation
  # outputs. Toolchain-agnostic — the caller passes `toolchain`.
  buildMoonbitPackage = import ./buildMoonbitPackage.nix { inherit lib stdenv; };
  buildMoonbitInterface = import ./buildMoonbitInterface.nix { inherit lib stdenv; };
  runMoonbitPrebuild = import ./runMoonbitPrebuild.nix { inherit lib stdenv; };
  linkMoonbitProgram = import ./linkMoonbitProgram.nix { inherit lib stdenv; };
  buildMoonbitRuntime = import ./buildMoonbitRuntime.nix { inherit stdenv zig; };
  makeMoonbitExecutable = import ./makeMoonbitExecutable.nix { inherit lib stdenv pkg-config zig; };
  buildMoonbitCStub = import ./buildMoonbitCStub.nix { inherit lib stdenv pkg-config zig; };
  buildMoonbitZigStub = import ./buildMoonbitZigStub.nix { inherit lib stdenv zig pkg-config; };
  translateMoonbitCHeader = import ./translateMoonbitCHeader.nix { inherit lib stdenv zig; };
  buildMoonbitObjcStub = import ./buildMoonbitObjcStub.nix { inherit stdenv clang; };
  archiveMoonbitStubs = import ./archiveMoonbitStubs.nix { inherit lib stdenv; };
in
{
  inherit
    buildCachedRegistry
    bundleWithRegistry
    buildMoonPackage
    buildMoonbitPackage
    buildMoonbitInterface
    runMoonbitPrebuild
    linkMoonbitProgram
    buildMoonbitRuntime
    makeMoonbitExecutable
    buildMoonbitCStub
    buildMoonbitZigStub
    translateMoonbitCHeader
    buildMoonbitObjcStub
    archiveMoonbitStubs
    ;
}
