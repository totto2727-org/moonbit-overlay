{
  inputs = {
    moon-registry = {
      url = "git+https://mooncakes.io/git/index?rev=c9c84f5ec832ad3ba7ffae330c6dad14775437fa";
      flake = false;
    };
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
    treefmt-nix = {
      url = "github:numtide/treefmt-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    {
      self,
      moon-registry,
      nixpkgs,
      treefmt-nix,
    }:
    let
      inherit (nixpkgs) lib;
      supportedSystems = [
        "x86_64-linux"
        "aarch64-darwin"
      ];
      forEachSystem = lib.genAttrs supportedSystems;

      minVersion = "0.6.28";
      deprecated = import ./deprecated.nix lib;

      overlay = (
        final: prev:
        let
          inherit (final) lib;
        in
        rec {
          moonbit-bin =
            (prev.moonbit-bin or { })
            // (import ./lib/moonbit-bin.nix {
              inherit lib minVersion;
              pkgs = final;
              versions = import ./versions.nix lib;
            }).legacyPackages
            // deprecated;
          moonbit-lang = final.callPackage ./lib/compiler.nix { };

          mkMoonPlatform = final.callPackage ./lib/moonPlatform {
            versions = import ./versions.nix lib;
          };
          moonPlatform = mkMoonPlatform { version = "latest"; };
          versions = import ./versions.nix lib;
        }
      );

      versions = import ./versions.nix lib;
      mkMoonbitBinPackages =
        pkgs:
        (import ./lib/moonbit-bin.nix {
          inherit
            lib
            pkgs
            versions
            minVersion
            ;
        }).packages;
      mkMoonbitBinLegacyPackages =
        pkgs:
        (import ./lib/moonbit-bin.nix {
          inherit
            lib
            pkgs
            versions
            minVersion
            ;
        }).legacyPackages;

      treefmtEval = forEachSystem (
        system: treefmt-nix.lib.evalModule (nixpkgs.legacyPackages.${system}) ./treefmt.nix
      );
    in
    {
      overlays = {
        default = overlay;
        moonbit-overlay = overlay;
      };

      packages = forEachSystem (
        system:
        let
          pkgs = nixpkgs.legacyPackages.${system};
        in
        mkMoonbitBinPackages pkgs
        // {
          default = self.packages.${system}.moonbit_latest;
        }
        // deprecated
        // {
          # compiler build from source
          # not used now
          compiler = pkgs.callPackage ./lib/compiler.nix { };
        }
      );
      legacyPackages = forEachSystem (
        system:
        let
          pkgs = nixpkgs.legacyPackages.${system};
        in
        mkMoonbitBinLegacyPackages pkgs // deprecated
      );

      apps = forEachSystem (
        system:
        let
          getMoonbit = lib.getExe' self.packages.${system}.default;
          mkMoonbitApp = name: {
            type = "app";
            program = getMoonbit name;
          };
        in
        {
          default = self.apps.${system}.moon;
        }
        // (lib.genAttrs [
          "moon"
          "moonx"
          "moonc"
          "mooncake"
          "moon_cove_report"
          "moondoc"
          "moonfmt"
          "mooninfo"
          "moonrun"
          "moon-lsp"
        ] mkMoonbitApp)
      );

      templates = rec {
        default = moonbit-dev;
        moonbit-dev = {
          path = ./moonbit-dev;
          description = "A startup basic MoonBit project";
        };
      };

      formatter = forEachSystem (system: treefmtEval.${system}.config.build.wrapper);
      checks = forEachSystem (
        system:
        let
          pkgs = import nixpkgs {
            inherit system;
            overlays = [ overlay ];
          };
          moonbit = pkgs.moonbit-bin.moonbit.latest;
        in
        {
          formatting = treefmtEval.${system}.config.build.check self;
          testToolchainHelpers = pkgs.runCommand "test-moonbit-toolchain-helpers" { } ''
            test -x ${moonbit}/bin/moon-lsp
            test -x ${moonbit}/bin/moon-ide

            # `moonx` is a symlink to `moon` (argv[0] dispatch, like the
            # official installer) and must expose the moonx CLI.
            test -L ${moonbit}/bin/moonx
            test -x ${moonbit}/bin/moonx
            ${moonbit}/bin/moonx --help | grep -Fq "Run a package from the Mooncakes registry"

            grep -Fq "export MOON_TOOLCHAIN_ROOT='${moonbit}'" ${moonbit}/bin/moon-lsp
            grep -Fq "export MOON_HOME='${moonbit}'" ${moonbit}/bin/moon-lsp
            grep -Fq "export MOON_TOOLCHAIN_ROOT='${moonbit}'" ${moonbit}/bin/moon-ide
            grep -Fq "export MOON_HOME='${moonbit}'" ${moonbit}/bin/moon-ide

            # Current toolchains use the `moon-lsp` name directly; do not add a
            # compatibility link for the old `moonbit-lsp` name.
            test ! -e ${moonbit}/bin/moonbit-lsp
            test ! -L ${moonbit}/bin/moonbit-lsp

            export PATH=${moonbit}/bin:$PATH
            export HOME=$TMPDIR/home
            mkdir -p "$HOME"
            unset MOON_HOME MOON_TOOLCHAIN_ROOT
            moon lsp --version >/dev/null
            moon ide --help >/dev/null

            touch $out
          '';
          # Run `nix build "#checks.<system>.testBuildMoonPackage"`
          testBuildMoonPackage = pkgs.moonPlatform.buildMoonPackage {
            name = "moonbit-overlay-test-with-deps";
            src = ./test/with_deps;
            moonMod = ./test/with_deps/moon.mod;
            moonRegistryIndex = moon-registry;
          };
          testMinimalMoonModConsumer = pkgs.callPackage ./test/minimal/package.nix {
            moonRegistryIndex = moon-registry;
          };
        }
      );
    };
}
